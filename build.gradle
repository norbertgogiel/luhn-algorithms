plugins {
    id 'java'
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '4.5.1'
    id 'maven-publish'
    id 'signing'
}

group 'com.vangogiel.luhnalgorithms'
version '1.0.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        html.enabled = true
        html.destination = file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = "COVEREDRATIO"
                minimum = 1.00
            }
        }
    }
}

spotless {
    java {
        importOrder("java", "javax", "com.norbertgogiel", "")
        removeUnusedImports()
        googleJavaFormat()
    }
}

publishing {
    java {
        withJavadocJar()
        withSourcesJar()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = 'Luhn algorithms for Luhn generation and validation'
                url = 'https://github.com/vangogiel/luhn-algorithms'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "vangogiel"
                        name = 'Norbert Gogiel'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vangogiel/luhn-algorithms.git'
                    developerConnection = 'scm:git:ssh://git@github.com/vangogiel/luhn-algorithms.git'
                    url = 'https://github.com/vangogiel/luhn-algorithms'
                }
            }
        }
    }
    repositories {
        maven {
            name = "mavencentral"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv('SONATYPE_NEXUS_USERNAME')
                password System.getenv('SONATYPE_NEXUS_PASSWORD')
            }
        }
    }
    signing {
        def signingKey = System.getenv('GPG_SIGNING_SUB_KEY')
        def signingPassword = System.getenv('GPG_SIGNING_PASSWORD')
        if (signingKey != null && signingPassword != null) {
            useInMemoryPgpKeys(signingKey.trim(), signingPassword.trim())
        }
        sign publishing.publications.mavenJava
    }
}

task devBuild {
    finalizedBy spotlessJavaApply, build, jacocoTestReport, jacocoTestCoverageVerification
}

task ciBuild {
    finalizedBy build, jacocoTestReport, jacocoTestCoverageVerification
}
